apply plugin: 'maven-publish'

def uploadScriptRootPath = gradle.ext.TEMP_UPLOAD_SCRIPT_ROOT_PATH
apply from: uploadScriptRootPath + "utils.gradle"
apply from: uploadScriptRootPath + "artifact-javadoc-handler.gradle"
apply from: uploadScriptRootPath + "artifact-pom-manager.gradle"

project.afterEvaluate {
    publishing {
        publications {
            mavenPublish(MavenPublication) {
                if (project.ext.mavProjectName) {
                    artifactId project.ext.mavProjectName
                }                 
                if (isAndroidProject()) {                   
                    artifact bundleReleaseAar
                    artifact androidJavadocsJar
                    artifact androidSourcesJar
                } else {
                    artifact jar
                    artifact sourcesJar
                    artifact javadocJar
                }
                
                decoratePom(pom)
            }
        }
        
        repositories {
            def isToRemoteRepo = getBooleanProperty("mavPublishToRemoteRepo")
            def isToInternalRepo = getBooleanProperty("mavPublishToInternalRepo")
            def isToMavenLocal = getBooleanProperty("mavPublishToMavenLocal")

            if (isToRemoteRepo) {
                maven {
                    credentials {
                        username getStringProperty("mavRemoteRepoUser")
                        password getStringProperty("mavRemoteRepoPassword")
                    }
                    url = getStringProperty("mavRepoRemoteUrl")
                }
            }

            if (isToInternalRepo) {
                maven {
                    url = getStringProperty("mavRepoInternalUrl")
                }
            }

            if (isToMavenLocal || (!isToInternalRepo && !isToRemoteRepo)) {
                mavenLocal()
            }
        }
    }
}
